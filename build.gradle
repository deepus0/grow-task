plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'war'
    id "com.moowork.node" version "1.3.1"
}

group = 'au.com.deep.grow'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.11'
archivesBaseName = 'grow-task'

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // Open Feign
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.1.0.RELEASE'
    runtime group: 'org.springframework.cloud', name: 'spring-cloud-dependencies', version: 'Hoxton.RELEASE', ext: 'pom'
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.7.0'
    implementation 'io.github.openfeign:feign-jackson:10.7.0'

    //Jasypt
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.0'

    // Xero
    implementation 'com.github.xeroapi:xero-java:4.9.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
}

node {
    version = "12.11.1"
    npmVersion = '6.9.0'
    download = true
    nodeModulesDir = file("${projectDir}/src/webapp")
}

def webappDir = "$projectDir/src/webapp"

sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
        }
    }
}

processResources {
    dependsOn "packageNpmApp"
}

task packageNpmApp(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = 'Compile client side assets for production'
    if (project.hasProperty('prod')) {
        args = ['run', 'build']
    } else {
        args = ['run', 'buildDev']
    }
}

